plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'org.jetbrains.kotlinx.kover' version '0.4.2'
    id 'application'
    id "com.github.ben-manes.versions" version '0.44.0'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.20'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.miglayout:miglayout-swing:5.2'
    implementation 'org.jfree:jfreechart:1.0.19'
    implementation 'com.mashape.unirest:unirest-java:1.4.9'
    implementation 'com.github.lgooddatepicker:LGoodDatePicker:10.3.1'
    implementation 'org.apache.derby:derby:10.14.2.0'
    implementation 'com.amazonaws:aws-java-sdk-elasticsearch:1.12.396'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.396'
    implementation 'com.github.awslabs:aws-request-signing-apache-interceptor:b3772780da'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:8.6.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2'
    implementation 'net.lingala.zip4j:zip4j:2.11.3'

    testImplementation "io.mockk:mockk:1.13.4"
    testImplementation 'io.kotest:kotest-assertions-core:5.5.4'
    testImplementation 'com.github.alexburlton:swing-test:1.0.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.2"
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "17"
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    kotlinOptions {
        jvmTarget = "17"
    }
}

test {
    useJUnitPlatform()

    jvmArgs '-Dcom.sun.management.jmxremote',
            '-Dcom.sun.management.jmxremote.port=9010',
            '-Dcom.sun.management.jmxremote.authenticate=false',
            '-Dcom.sun.management.jmxremote.ssl=false',
            '-Djava.rmi.server.hostname=localhost',
            '-DscreenshotOs=linux',
            '--add-opens', 'java.desktop/java.awt.event=ALL-UNNAMED',
            '--add-opens', 'java.desktop/java.awt=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED'
}

tasks.withType(Test) {
    minHeapSize = "512m"
    maxHeapSize = "512m"

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task runDev(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    args 'devMode', 'trueLaunch'
    main 'dartzee.main.DartsMainKt'
}

task unitTest(type: Test) {
    useJUnitPlatform {
        excludeTags "integration", "e2e"
    }
}

task updateScreenshots(type: Test) {
    jvmArgs '-DupdateSnapshots=true'
    useJUnitPlatform {
        includeTags "screenshot"
    }
}

task integrationAndE2E(type: Test) {
    useJUnitPlatform {
        includeTags "integration", "e2e"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'dartzee.main.DartsMainKt'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

mainClassName = 'dartzee.main.DartsMainKt'